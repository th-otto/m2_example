(****************************************
*                                       *
*     MODULA-2 Multi-Pass Compiler      *
*     ****************************      *
*                                       *
*     Implementation for Lilith         *
*                                       *
*                                       *
*     MCBase:                           *
*                                       *
*     private part of the common base   *
*     of the Modula-2 compiler          *
*                                       *
*     Version C18 of 22.12.81           *
*                                       *
*     Institut fuer Informatik          *
*     ETH-Zuerich                       *
*     CH-8092 Zuerich                   *
*                                       *
****************************************)

DEFINITION MODULE MCBase;     (* LG / CHJ *)

FROM SYSTEM IMPORT ADDRESS;

  EXPORT QUALIFIED
    levmax, modnamlength,
    maxprio, noprio,
    Idptr, Stptr, Listptr, Stringptr,
    Idclass, Idset, Structform, Stset,
    Recpart, Varkind, Kindvar, Stpures, Stfuncs,
    Spellix, Levrange, Constval, Realval, Modnamarr, Keyarr,
    boolptr, charptr, intptr, cardptr, intcarptr, realptr, longrealptr, anyrealptr,
    procptr, bitsetptr, longbitsetptr, byteptr, wordptr, longwordptr, addrptr, processptr, niltypeptr, sgnintsptr, longintptr, longcardptr,
    substptr, strptrs, root, mainmodp, sysmodp,
    globvarnext, procnumber, stringcount, stringroot,
    Symbol;

  CONST
    levmax = 15;          (* maximal nesting level *)
    modnamlength = 24;    (* maximal length for module identifiers *)
    maxprio = 15;         (* maximal priority allowed for Lilith *)
    noprio = maxprio + 1; (* priority value for no priority *)

  TYPE
    Idptr      = POINTER TO Identrec;
    Stptr      = POINTER TO Structrec;

    Structform = (enums,      (* 0 *)
                  bools,      (* 1 *)
                  chars,      (* 2 *)
                  sgnints,    (* 3 *)
                  intcards,   (* 4 *)
                  ints,       (* 5 *)
                  cards,      (* 6 *)
                  longints,   (* 7 *)
                  longcards,  (* 8 *)
                  bytes,      (* 9 *)
                  words,      (* 10 *)
                  longwords,  (* 11 *)
                  subranges,  (* 12 *)
                  addrs,      (* 13 *)
                  reals,      (* 14 *)
                  longreals,  (* 15 *)
                  anyreals,   (* 16 *)
                  pointers,   (* 17 *)
                  sets,       (* 18 *)
                  proctypes,  (* 19 *)
                  arrays,     (* 20 *)
                  records,    (* 21 *)
                  hides,      (* 22 *)
                  opens);     (* 23 *)
    Stset      = SET OF Structform;
    Idclass    = (consts, types, vars, fields, pures, funcs, mods, inlines, unknown, indrct);
    Idset      = SET OF Idclass;
    Recpart    = (fixedpart, tagfield, variantpart);

    Structrec  = RECORD
                   size: LONGCARD; (* word size *)
                   stidp: Idptr; (* identifier defining this structure *)
                   inlist: BOOLEAN; (* structure entered into a list *)
                   CASE form: Structform OF
                     bools,chars,ints,cards,words,reals: (* no field *)
                    |enums: fcstp: Idptr; cstnr: CARDINAL
                    |subranges: scalp: Stptr; min, max: CARDINAL
                    |pointers: elemp: Stptr
                    |sets: basep: Stptr
                    |arrays:
                       elp, ixp: Stptr;
                       dyn: BOOLEAN;
                    |records:
                       CASE rpart: Recpart OF
                         fixedpart: fieldp: Idptr; tagp: Stptr
                        |tagfield: fstvarp, elsevarp: Stptr; tagtyp: Stptr
                        |variantpart: nxtvarp, subtagp: Stptr; varval: CARDINAL
                         END (* case Recpart *)
                    |proctypes:
                       fstparam : Idptr; (* pointer to parameter list *)
                       CASE rkind: Idclass OF
                         funcs : funcp : Stptr; (* pointer to function type *)
                        |pures : (* no further fields *)
                       END;
                    |hides,opens: (* conversion from hides to opens *)
                       openstruc : Stptr; (* used for opens *)
                   END (* case Structform *)
                 END (**record**);

    Varkind    = (noparam, valparam, varparam, copyparam);
    Kindvar    = (global, local, absolute, separate);
    Spellix    = CARDINAL;
    Listptr    = POINTER TO Listrec;
    Listrec    = RECORD
                   element : Idptr;
                   next : Listptr;
                 END;
    Stringptr  = POINTER TO Stringval;
    Stringval  = RECORD
                   loadoffset : LONGCARD;
                   valentry : ADDRESS;
                   slink : Stringptr;
                   (* loadoffset means the offset in the     *)
                   (* string data area of the loaded program *)
                   (* valentry is an address to the heap     *)
                   (* entry of the string value              *)
                   (* the structure of valentry should be:   *)
                   (*   POINTER TO ARRAY [0..length] OF CHAR *)
                   (* the element with index length is a 0C  *)
                   (* slink links the strings to be loaded   *)
                 END;
    Realval =  RECORD
                  CASE BOOLEAN OF
                    FALSE : ra : ARRAY [0..3] OF CARDINAL;
                   |TRUE : rc : REAL;
                  END;
                END;
    Constval   = RECORD
                   CASE : Structform OF
                     arrays: (* for string constants only *)
                       svalue : Stringptr;
                    |reals: rvalue : POINTER TO Realval;
                    |longcards: lvalue: LONGCARD;
                    |words: wvalue: ARRAY[0..1] OF CARDINAL;
                   ELSE (* oneword constants *)
                     value : CARDINAL;
                   END;
                 END;
    Keyarr     = ARRAY [0..2] OF CARDINAL;
    Modnamarr  = ARRAY [0..modnamlength-1] OF CHAR;
    Levrange   = [0..levmax];
    Stpures    = (codep,  (* 0 *)
                  decp,   (* 1 *)
                  disposep,(* 2 *)
                  exclp,  (* 3 *)
                  haltp,  (* 4 *)
                  incp,   (* 5 *)
                  inclp,  (* 6 *)
                  iotrsp, (* 7 *)
                  listenp,(* 8 *)
                  newp,   (* 9 *)
                  nprp,   (* 10 *)
                  setregp,(* 11 *)
                  trsp,   (* 12 *)
                  pushp,  (* 13 *)
                  popp,   (* 14 *)
                  superp, (* 15 *)
                  userp   (* 16 *)
                  );
    Stfuncs    = (absf,   (* 0 *)
                  adrf,   (* 1 *)
                  ashf,   (* 2 *)
                  capf,   (* 3 *)
                  chrf,   (* 4 *)
                  fltf,   (* 5 *)
                  higf,   (* 6 *)
                  oddf,   (* 7 *)
                  ordf,   (* 8 *)
                  registerf, (* 9 *)
                  sizf,   (* 10 *)
                  trcf,   (* 11 *)
                  tsizef, (* 12 *)
                  valf,   (* 13 *)
                  maxf,   (* 14 *)
                  minf,   (* 15 *)
                  shortf, (* 16 *)
                  longf,  (* 17 *)
                  ltrcf,  (* 18 *)
                  lfltf   (* 19 *)
                  );
    Identrec   = RECORD
                   (* 0 *) name: Spellix;
                   (* 2 *) link: Idptr;
                   (* 6 *) CASE :BOOLEAN OF
                     FALSE: nxtidp: Idptr
                    |TRUE : idtyp : Stptr
                   END (* case BOOLEAN *);
                   (* 10 *) globmodp: Idptr; (* pointer to global module *)
                   (* 14 *) lineno: CARDINAL;
                   (* 16 *) CASE klass: Idclass OF
                     types: (* no further fields *)
                    |consts,unknown: (* unknown may convert to consts *)
                       cvalue: Constval;
                    |vars:
                       indaccess : BOOLEAN; (* indirect access to value *)
                       state : Kindvar;
                       vkind : Varkind;
                       vlevel : Levrange;
                       vaddr : LONGCARD; (* offset *)
                       vlink : Idptr;     (* variables or parameters *)
                    |fields: fldaddr: CARDINAL
                    |pures, funcs, mods:
                       CASE isstandard: BOOLEAN OF
                         TRUE:
                           CASE :Idclass OF
                             pures: pname: Stpures
                            |funcs: fname: Stfuncs
                           END (* case Idclass *)
                        |FALSE:
                           procnum : CARDINAL;
                           locp : Idptr;
                           msp : Listptr;
                           plev : Levrange;
                           varlength : CARDINAL;
                           priolev : CARDINAL;
                           externalaccess : BOOLEAN;
                           CASE :Idclass OF
                             pures,funcs:
                               CASE codeproc : BOOLEAN OF
                                 FALSE:
                                   locvarp : Idptr; (* local variables, no parameters *)
                                |TRUE:
                                   codelength : CARDINAL;
                                   codeentry : CARDINAL;
                                   (* codeentry is a heap entry of code values *)
                                   (* belonging to the code procedure          *)
                                   (* it represents the following structure:   *)
                                   (*   POINTER TO                             *)
                                   (*     ARRAY [1..codelength] OF CARDINAL    *)
                               END; (* CASE *)
                            |mods:
                               impp: Listptr;
                               expp: Idptr;
                               qualexp: BOOLEAN;
                               CASE globalmodule : BOOLEAN OF
                                 FALSE: (* no further field *)
                                |TRUE:
                                   globvarp : Idptr; (* global variables *)
                                   modnum : CARDINAL;
                                   modulekey : Keyarr;
                                   identifier : Modnamarr;
                               END; (* case globalmodule *)
                           END; (* case Idclass *)
                       END (* case isstandard *);
                    |indrct: (* no further fields *)
                   END (* case Idclass *)
                 END (**record**);

  VAR
    boolptr    : Stptr;   (* structure of type BOOLEAN *)
    charptr    : Stptr;   (* structure of type CHAR    *)
    intptr     : Stptr;   (* structure of type INTEGER *)
    cardptr    : Stptr;   (* structure of type CARDINAL *)
    intcarptr  : Stptr;   (* structure of intcar - type *)
    sgnintsptr : Stptr;
    longintptr : Stptr;   (* structure of type LONGINT *)
    longcardptr: Stptr;   (* structure of type LONGCARD *)
    realptr    : Stptr;   (* structure of type REAL *)
    longrealptr: Stptr;   (* structure of type LONGREAL *)
    procptr    : Stptr;   (* structure of type PROC *)
    bitsetptr  : Stptr;   (* structure of type BITSET  *)
    longbitsetptr  : Stptr; (* structure of type LONGBITSET  *)
    anyrealptr : Stptr;
    byteptr    : Stptr;   (* structure of type BYTE *)
    wordptr    : Stptr;   (* structure of type WORD *)
    longwordptr: Stptr;   (* structure of type LONGWORD *)
    addrptr    : Stptr;   (* structure of type ADDRESS *)
    processptr : Stptr;   (* structure of type PROCESS *)
    niltypeptr : Stptr;
    strptrs    : ARRAY [0 .. 20] OF Stptr;
                          (* table to string-structure entries *)
    substptr   : Idptr;   (* list of procedures to be substituted *)
    root       : Idptr;   (* root of standardname entries  *)
    mainmodp   : Idptr;   (* pointer to main module *)
    sysmodp    : Idptr;   (* pointer to module SYSTEM *)
    globvarnext: CARDINAL;(* next address for global variables *)
    procnumber : CARDINAL;(* number of procedures in program *)
    stringcount: CARDINAL;(* number of bytes needed for string area *)
    stringroot : Stringptr;(* chain of strings to be loaded *)

TYPE Symbol = (
(* pass 1 *)
  eop,        (* 00 *)
  andsy,      (* 01 *)
  divsy,      (* 02 *)
  times,      (* 03 *)
  slash,      (* 04 *)
  modsy,      (* 05 *)
  notsy,      (* 06 *)
  plus,       (* 07 *)
  minus,      (* 08 *)
  orsy,       (* 09 *)
  eql,        (* 0a *)
  neq,        (* 0b *)
  grt,        (* 0c *)
  geq,        (* 0d *)
  lss,        (* 0e *)
  leq,        (* 0f *)
  insy,       (* 10 *)
  lparent,    (* 11 *)
  rparent,    (* 12 *)
  lbrack,     (* 13 *)
  rbrack,     (* 14 *)
  lconbr,     (* 15 *)
  rconbr,     (* 16 *)
  comma,      (* 17 *)
  semicolon,  (* 18 *)
  period,     (* 19 *)
  colon,      (* 1a *)
  range,      (* 1b *)
  constsy,    (* 1c *)
  typesy,     (* 1d *)
  varsy,      (* 1e *)
  arraysy,    (* 1f *)
  recordsy,   (* 20 *)
  variant,    (* 21 *)
  setsy,      (* 22 *)
  pointersy,  (* 23 *)
  tosy,       (* 24 *)
  arrow,      (* 25 *)
  hidden,     (* 26 *)
  importsy,   (* 27 *)
  exportsy,   (* 28 *)
  fromsy,     (* 29 *)
  qualifiedsy,(* 2a *)
  beginsy,    (* 2b *)
  casesy,     (* 2c *)
  ofsy,       (* 2d *)
  ifsy,       (* 2e *)
  thensy,     (* 2f *)
  elsifsy,    (* 30 *)
  elsesy,     (* 31 *)
  loopsy,     (* 32 *)
  exitsy,     (* 33 *)
  repeatsy,   (* 34 *)
  untilsy,    (* 35 *)
  whilesy,    (* 36 *)
  dosy,       (* 37 *)
  withsy,     (* 38 *)
  forsy,      (* 39 *)
  bysy,       (* 3a *)
  returnsy,   (* 3b *)
  codesy,     (* 3c *)
  unused3d,
  becomes,    (* 3e *)
  endsy,      (* 3f *)
  call,       (* 40 *)
  endblock,   (* 41 *)
  definitionsy, (* 42 *)
  implementationsy, (* 43 *)
  proceduresy, (* 44 *)
  modulesy,   (* 45 *)
  symbolsy,   (* 46 *)
  ident,      (* 47 *)
  inlinesy,   (* 48 *)
  intcon,     (* 49 *)
  cardcon,    (* 4a *)
  intcarcon,  (* 4b *)
  realcon,    (* 4c *)
  charcon,    (* 4d *)
  stringcon,  (* 4e *)
  option,     (* 4f *)
  errorsy,    (* 50 *)
  eol,        (* 51 *)
(* pass 2 *)
  namesy,     (* 52 *)
(* pass 3 *)
  field,      (* 53 *)
  anycon);    (* 54 *)

END MCBase.
